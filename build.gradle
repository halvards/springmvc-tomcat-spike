apply plugin: 'application'     // http://www.gradle.org/docs/current/userguide/application_plugin.html
apply plugin: 'eclipse'         // http://www.gradle.org/docs/current/userguide/eclipse_plugin.html
apply plugin: 'idea'            // http://www.gradle.org/docs/current/userguide/idea_plugin.html
apply plugin: 'java'            // http://www.gradle.org/docs/current/userguide/java_plugin.html

def javaVersion = '1.8'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

mainClassName = 'com.skogsrud.halvard.springmvc.spike.tomcat.Server'

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

def springVersion = '4.2.0.RELEASE'
def tomcatVersion = '8.0.24'

def junitVersion = '4.12'
def hamcrestVersion = '1.3'
def okhttpVersion = '2.4.0'

dependencies {
    compile "org.springframework:spring-webmvc:${springVersion}",
            "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-log4j:${tomcatVersion}"

    testCompile "junit:junit:${junitVersion}",
            "org.hamcrest:hamcrest-library:${hamcrestVersion}",
            "com.squareup.okhttp:okhttp:${okhttpVersion}"
}

task onejar(type: Jar) {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

idea.module {
    downloadSources = true
    downloadJavadoc = false
}

idea.project {
    jdkName javaVersion
    languageLevel javaVersion
}
